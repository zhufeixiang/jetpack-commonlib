plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp' version '1.9.24-1.0.20'
    id 'maven-publish'
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.zfx.commonlib'
                artifactId = 'commonlib'
                version = '2.0.0'
            }
        }
    }
}

android {
    namespace 'com.zfx.commonlib'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode 3
        versionName '2.0.0'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding = true
        dataBinding = true
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = '1.5.14'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    testImplementation 'junit:junit:4.13.2'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    api 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    api 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.7'
    api 'androidx.activity:activity-ktx:1.9.0'
    api 'androidx.fragment:fragment-ktx:1.8.5'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.3.2'
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'
    // Retrofit 3.0.0+ 已内置协程支持，无需 retrofit2-kotlin-coroutines-adapter
    //动态替换BaseUrl库 使用可参考 https://github.com/JessYanCoding/RetrofitUrlManager
    implementation 'me.jessyan:retrofit-url-manager:1.4.0'

    implementation 'com.google.code.gson:gson:2.13.2'

    // 图片加载库建议在项目中使用 Coil 或其他库，不在基础库中提供
    // 原因：图片加载库的选择是项目级别的决策，不应该强制绑定在基础库中

    //recyclerview 万能适配器 https://github.com/CymChad/BaseRecyclerViewAdapterHelper
    api 'io.github.cymchad:BaseRecyclerViewAdapterHelper4:4.3.2'

    //https://github.com/Blankj/AndroidUtilCode 通用工具库
    api 'com.blankj:utilcodex:1.31.1'

    // 日志库 https://github.com/orhanobut/logger
    api 'com.orhanobut:logger:2.2.0'
    //各种弹框 https://github.com/li-xiaojun/XPopup
    api 'com.github.li-xiaojun:XPopup:2.10.0'
    //android 4.4以上沉浸式状态栏和沉浸式导航栏管理 https://github.com/gyf-dev/ImmersionBar
    api 'com.geyifeng.immersionbar:immersionbar:3.2.2'
    api 'com.geyifeng.immersionbar:immersionbar-ktx:3.2.2'
    //头条屏幕适配方案 https://github.com/JessYanCoding/AndroidAutoSize
    api 'com.github.JessYanCoding:AndroidAutoSize:v1.2.1'
    //权限请求框架 https://github.com/getActivity/XXPermissions
    api 'com.github.getActivity:XXPermissions:25.2'

    api 'org.greenrobot:eventbus:3.3.1'

    // Compose
    api platform('androidx.compose:compose-bom:2024.06.00')
    api 'androidx.compose.ui:ui'
    api 'androidx.compose.ui:ui-tooling-preview'
    api 'androidx.compose.material3:material3'
    api 'androidx.activity:activity-compose:1.9.0'
    api 'androidx.lifecycle:lifecycle-runtime-compose:2.6.2'
    debugImplementation 'androidx.compose.ui:ui-tooling'


}